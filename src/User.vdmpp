class User

types
	
	public String = seq of char;

instance variables

	private name: String := [];
	private city: String := [];
	private balance: rat := 0;
	
operations

	-- constructor
	public User: String * String ==> User
			User(userName, userCity) == (
				name := userName;
				city := userCity;
				return self;
	)
	pre len userName > 0 and len userCity > 0;
	
	-- returns the user's name
	pure public getName: () ==> String
		getName() == (
			return name;
		);
		
	-- returns the user's balance
	pure public getBalance: () ==> rat
		getBalance() == (
			return balance;
		);
		
	-- returns the user's city
	pure public getCity: () ==> String
		getCity() == (
			return city;
		);
		
	-- charges the user an amount
	public charge: rat ==> ()
		charge(amount) == (
			balance := balance - amount;
		)
		pre amount <= balance and amount > 0;
		
	-- deposits to the user's balance an amount
	public deposit: rat ==> ()
		deposit(amount) == (
			balance := balance + amount;
		)
		pre amount > 0;
			
end User