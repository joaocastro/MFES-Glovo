class Order

types

	public String = seq of char;
	public TimeStamp :: minutes : nat
											seconds : nat;
	public State = <waiting> | <delivering> | <delivered>;

instance variables

	private deliveryAddress: String := [];
	private deliveryTime: TimeStamp;
	private items: seq of Item := [];
	private user: User;
	private seller: Seller;
	private state: State := <waiting>;
	private driver: Driver;
	private emptyDriver:Driver := new Driver("Null","Null");

operations

	public Order : String * TimeStamp * seq of Item * User * Seller ==> Order
		Order (orderDeliveryAddress, orderDeliveryTime, orderItems, orderUser, orderSeller) == (
			deliveryAddress := orderDeliveryAddress;
			deliveryTime := orderDeliveryTime;
			items := orderItems;
			user := orderUser;
			seller := orderSeller;
			driver := emptyDriver;
			return self
		)
	pre len orderDeliveryAddress > 0 and 
		len orderItems > 0 and 
		orderSeller.getCity() = orderUser.getCity() and
		forall item in set elems orderItems & item in set { i | i in set elems orderSeller.getItems()};

	-- returns the order's delivery address
	pure public getDeliveryAddress : () ==> String
		getDeliveryAddress () == (
			return deliveryAddress;
	);
	
	-- returns the order's total price
	pure public getTotalPrice : () ==> rat
		getTotalPrice () == (
			dcl totalPrice : rat := seller.getDeliveryPrice();
			
			for item in items do (
				totalPrice := totalPrice + item.getPrice();
			);
			
			return totalPrice;
	);
	
	-- returns the order's delivery time
	pure public getDeliveryTime : () ==> TimeStamp
		getDeliveryTime () == (
			return deliveryTime;
	);
	
	-- returns the order's items
	pure public getItems : () ==> seq of Item
		getItems () == (
			return items;
	);
	
	-- returns the order's seller
	pure public getSeller : () ==> Seller
		getSeller () == (
			return seller;
	);
	
	-- returns the order's seller
	pure public getUser : () ==> User
		getUser () == (
			return user;
	);
	
	-- returns the order's state
	pure public getState : () ==> State
		getState () == (
			return state;
	);
	
	-- returns the order's driver
	pure public getDriver : () ==> Driver
		getDriver () == (
			return driver;
	);
	
	-- assigns a driver to the order and start's the delivery process
	public startDelivery : Driver ==> ()
		startDelivery (assignee) == (
			user.charge(getTotalPrice());
			driver := assignee;
			driver.makeDelivering();
			state := <delivering>;
		)
	pre state = <waiting> and
		assignee.getCity() = user.getCity() and
		assignee.getStatus() = <available> and
		user.getBalance() > getTotalPrice();
	
	-- finishes the delivery process
	public finishDelivery : () ==> ()
		finishDelivery () == (
			driver.makeAvailable();
			state := <delivered>;
		)
	pre state = <delivering>
	post driver.getStatus() = <available>;

end Order