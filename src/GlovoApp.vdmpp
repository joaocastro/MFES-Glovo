class GlovoApp

types
	
	public String = seq of char;

instance variables

	private sellers: seq of Seller := [];
	private drivers: seq of Driver := [];
	private users: seq of User := [];
	private orders: seq of Order := [];
	inv forall order in set elems orders & order.getUser() in set elems users;
	inv forall order in set elems orders & order.getSeller() in set elems sellers;

operations

	-- default constructor
	public GlovoApp : () ==> GlovoApp
		GlovoApp () == (
			return self
		);

	-- returns all the available sellers
	pure public getSellers : () ==> seq of Seller
		getSellers () == (
			return sellers;
	);
	
	-- returns a seller by name
	pure public getSellerByName : String ==> Seller
		getSellerByName (name) == (
			dcl seller : Seller;
			for s in sellers do (
				if s.getName() = name then seller := s;
			);
			
			return seller;
		)
		pre name in set { s.getName() | s in set elems sellers };
	
	-- returns all the drivers
	pure public getDrivers : () ==> seq of Driver
		getDrivers () == (
			return drivers;
	);
	
	-- returns all the available drivers in city
	pure public getAvailableDriversByCity : String ==> seq of Driver
		getAvailableDriversByCity (city) == (
			dcl availableDrivers : seq of Driver := [];
			
			for driver in drivers do (
				if driver.getStatus() = <available> and driver.getCity() = city then
					availableDrivers := availableDrivers ^ [driver];
			);
			
			return availableDrivers;
		);
	
	-- returns all the users
	pure public getUsers : () ==> seq of User
		getUsers () == (
			return users;
	);
	
	-- returns a user by name
	pure public getUserByName : String ==> User
		getUserByName (name) == (
			dcl user : User;
			for u in users do (
				if u.getName() = name then user := u;
			);
			
			return user;
		)
		pre name in set { u.getName() | u in set elems users };

	-- returns all the available orders
	pure public getOrders : () ==> seq of Order
		getOrders () == (
			return orders;
	);
	
	-- returns all the available orders
	pure public getWaitingOrders : () ==> seq of Order
		getWaitingOrders () == (
			dcl waitingOrders : seq of Order := [];
			
			for order in orders do (
				if order.getState() = <waiting> then waitingOrders := waitingOrders ^ [order];
			);
			
			return waitingOrders;
	);
	
	-- adds a seller to Glovo
	public addSeller : Seller ==> ()
		addSeller (newSeller) == (
			sellers := sellers ^ [newSeller];
		)
		pre forall seller in set elems sellers & seller.getName() <> newSeller.getName();
	
	-- adds a driver to Glovo
	public addDriver : Driver ==> ()
		addDriver (newDriver) == (
			drivers := drivers ^ [newDriver];
		)
		pre forall driver in set elems drivers & driver.getName() <> newDriver.getName();
		
	-- registers a user in Glovo
	public registerUser : User ==> ()
		registerUser (newUser) == (
			users := users ^ [newUser];
		)
		pre forall user in set elems users & user.getName() <> newUser.getName();
		
	-- makes a new order
	public addOrder : Order ==> bool
		addOrder (order) == (			
			dcl availableDrivers : seq of Driver;

			orders := orders ^ [order];
			
			availableDrivers := getAvailableDriversByCity(order.getUser().getCity());
			if len availableDrivers > 0 then (
				order.startDelivery(hd availableDrivers);
				return true
			);
			
			return false;	
		)
		pre order.getUser() in set elems users;
	
	-- triggers a wating order if there is any
	public triggerWaitingOrder : () ==> Order
		triggerWaitingOrder () == (
			dcl order : Order;
			dcl availableDrivers : seq of Driver;
			
			if len getWaitingOrders() > 0 then (
				order := hd getWaitingOrders();
				
				availableDrivers := getAvailableDriversByCity(order.getUser().getCity());
				if len availableDrivers > 0 then (
					order.startDelivery(hd availableDrivers);
				);			
			);

			return order;
		)
		pre len getWaitingOrders() > 0 and len getAvailableDriversByCity((hd getWaitingOrders()).getUser().getCity()) > 0;
		
	-- adds an item to a seller by name
	public addItemToSeller : String * Item ==> ()
		addItemToSeller (sellerName, item) == (
			for seller in sellers do (
				if seller.getName() = sellerName then seller.addItem(item);
			)
		);
		
	-- returns all restaurants in a city
	public getRestaurantsByCity : String ==> seq of Restaurant
		getRestaurantsByCity(city) == (
			dcl restaurants : seq of Restaurant := [];
		
			for seller in sellers do (
					if is_(seller, Restaurant) and seller.getCity() = city then restaurants := restaurants ^ [seller];
			);
			
			return restaurants;
		)
		post forall restaurant in set elems RESULT & is_(restaurant, Restaurant);
		
	-- returns all stores in a city
	public getStoresByCity : String ==> seq of Store
		getStoresByCity(city) == (
			dcl stores : seq of Store := [];
		
			for seller in sellers do (
					if is_(seller, Store) and seller.getCity() = city then stores := stores ^ [seller];
			);
			
			return stores;
		)
		post forall store in set elems RESULT & is_(store, Store);

end GlovoApp